plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.10.0'
}

allure {
    report {
        version.set("2.20.0")
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set("2.19.0") //версия интеграции фреймворка и Allure
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation (
            "com.github.javafaker:javafaker:1.0.2",
            'com.codeborne:selenide:6.10.3',
            'io.qameta.allure:allure-selenide:2.20.0',
            "org.junit.jupiter:junit-jupiter:5.9.1",
            'org.slf4j:slf4j-simple:2.0.5',
            "io.github.bonigarcia:webdrivermanager:5.3.1"
    )
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task smoke_tests(type: Test) {
    useJUnitPlatform {
        includeTags("smoke")
    }
}
task one_property_tests(type: Test) {
    useJUnitPlatform {
        includeTags("one_property")
    }
}

help {
    doFirst {
        println ""
        println "************************************************************************ "
        println "***                                                                  *** "
        println "*** Welcome to https://github.com/qa-guru/demoqa-test-16!            *** "
        println "***                                                                  *** "
        println "*** ./gradlew help                                                   *** "
        println "***                                                                  *** "
        println "************************************************************************ "
        println ""
    }
}